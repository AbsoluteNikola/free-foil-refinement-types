
token VarIdent lower (letter | digit | '_')* ;

-- expr terms
ConstInt.     Term ::= Integer ;
Bool.         Term ::= ConstBool ;
Var.          Term ::= VarIdent ;
Let.          Term ::= Decl ScopedTerm ;
Fun.          Term ::= "(" VarIdent ")" "=>" "{" ScopedTerm "}" ;
App.          Term ::= Term "(" FuncAppArg ")"; -- Program should be in ANF. See page 14
Op.           Term ::= FuncAppArg IntOp FuncAppArg; -- Program should be in ANF. See page 14
_.            Term ::= "(" Term ")";

ConstTrue.    ConstBool ::= "true";
ConstFalse.   ConstBool ::= "false";

Annotation.          Annotation ::= "/*@" "val" VarIdent ":" RType "*/";

PlainDecl.  PlainDecl ::= "let" VarIdent "=" Term ";";

AnnotatedDecl.         Decl ::= Annotation PlainDecl;
UnAnnotatedDecl.       Decl ::= PlainDecl;
separator Decl "";

IntPlus. IntOp ::= "+";
IntMinus. IntOp ::= "-";
IntMultiply. IntOp ::= "*";

-- type terms
TypeRefined.      RType ::= BaseType "[" VarIdent "|" Pred "]" ;
TypeFun.          RType ::= FuncArg "=>" ScopedRType ;   -- FIXME: same problem as with Let
_.                RType ::= "(" RType ")";
ScopedRType.      ScopedRType ::= RType;

NamedFuncArg.   FuncArg ::= VarIdent ":" RType;

-- predicates
PVar.           Pred ::= VarIdent;
PBool.          Pred ::= ConstBool ;
PInt.           Pred ::= Integer ;
PEq.            Pred ::= Pred "==" Pred ;
PLessThan.      Pred ::= Pred "<" Pred ;
PLessOrEqThan.  Pred ::= Pred "<=" Pred ;
PGreaterThan.  Pred ::= Pred ">" Pred ;
PGreaterOrEqThan.  Pred ::= Pred ">=" Pred ;
PPlus.          Pred ::= Pred "+" Pred ;
PMinus.         Pred ::= Pred "-" Pred ;
PMultiply.      Pred ::= Pred "*" Pred ;

PatternVar. Pattern ::= VarIdent ;
ScopedTerm. ScopedTerm ::= Term ;


BaseTypeInt.  BaseType ::= "int" ;
BaseTypeBool.  BaseType ::= "bool" ;

-- FunArgName. FunArgName ::= VarIdent;
-- separator nonempty FunArgsName "," ;
-- ArgList. ArgList ::= Term ;
-- separator nonempty ArgList "," ;

FuncAppArgBool. FuncAppArg ::= ConstBool;
FuncAppArgInt. FuncAppArg ::= Integer;
FuncAppArgVar. FuncAppArg ::= VarIdent;

entrypoints Term;
